\ Copyright (C) 2009 Rafael R. Sevilla
\
\ This file is part of CArc
\
\ CArc is free software; you can redistribute it and/or modify it
\ under the terms of the GNU Lesser General Public License as
\ published by the Free Software Foundation; either version 3 of the
\ License, or (at your option) any later version.
\
\ This library is distributed in the hope that it will be useful,
\ but WITHOUT ANY WARRANTY; without even the implied warranty of
\ MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
\ GNU Lesser General Public License for more details.
\
\ You should have received a copy of the GNU Lesser General Public
\ License along with this library; if not, write to the Free Software
\ Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston MA
\ 02110-1301 USA.

\E stack data-stack sp value

\E inst-stream stack-prefix #

\E s" value"   single data-stack type-prefix i

\E s" Inst *"  single data-stack type-prefix target

\ No operation
nop ( -- )

\ Push a value in the value register into the stack
push ( -- i )
i = t->valr;

\ Pop the stack, and place its topmost value into the value reg.
pop ( i -- )
t->valr = i;

\ The ldl instruction loads a literal value from the literal values
\ in the funr register into value register.  The instruction argument
\ is its index into the funr vector and should in most cases be 3 or
\ higher for normal compiled code.
ldl ( #i2 -- )
t->valr = VINDEX(t->funr, i2);

\ Load an immediate value (usually a fixnum) into the value register
ldi ( #i2 -- )
t->valr = i2;

\ Load a global value.  The value in the instruction is generally a
\ symbol, and indexes into the global symbol table.
ldg ( #i2 -- )
{
  value tmp;

  tmp = VINDEX(t->funr, i2);
  if ((t->valr = carc_hash_lookup(c, c->genv, tmp)) == CUNBOUND) {
    c->signal_error(c, "Unbound symbol %S\n", tmp);
    t->valr = CNIL;
  }
}

\ Set a global value with the value of the value register
stg ( #i2 -- )
carc_hash_insert(c, c->genv, VINDEX(t->funr, i2), t->valr);

\ Load the value of a variable in the environment.  The first
\ operand is the number of the environment, and the second is
\ the index into that environment.
lde ( #ienv #iindx -- )
{
  value tmp = t->envr;
  while (--ienv >= 0) tmp = cdr(tmp);
  t->valr = VINDEX(car(tmp), iindx);
}

\ Store to an environment variable
ste ( #ienv #iindx -- )
{
  value tmp = t->envr;
  while (--ienv >= 0) tmp = cdr(tmp);
  WB(&VINDEX(car(tmp), iindx), t->valr);
}

\ Make a continuation at offset icofs, placing it in the value register.
\ Note that this is a relative address, but the absolute address is
\ stored in the continuation.
cont ( #icofs -- )
WB(&t->valr, carc_mkcont(c, INT2FIX(icofs), t->funr, t->envr));

\ Make an environment of a given size
env ( #ienvsize -- )
WB(&t->envr, carc_mkenv(c, t->envr, ienvsize));
WB(&VINDEX(car(t->envr), 0), VINDEX(t->funr, 2));

\ apply a function
apply ( #iargc -- )
t->argc = iargc;
carc_apply(c, thr, t->valr);
goto restart;

ret ( -- )
carc_return(c, thr, t->valr);
goto restart;

\ Jump unconditional
jmp ( #target -- )
SET_IP(target);

\ Jump if valr is not nil
jt ( #target -- )
if (t->valr != CNIL) {
  SET_IP(target);
  INST_TAIL;
}

\ Jump if valr is nil
jf ( #target -- )
if (t->valr == CNIL) {
  SET_IP(target);
  INST_TAIL;
}

true ( -- )
t->valr = CTRUE;

nil ( -- )
t->valr = CNIL;

\ Halt execution of this virtual machine thread
hlt ( -- )
t->state = Trelease;
goto endquantum;

\ Maybe we can do without the instructions below this point, and
\ make everything a function call, however that would probably be
\ very slow until we can figure out how to do inline caching
\ properly.
add  ( i -- )
t->valr = __carc_add2(c, t->valr, i);

sub ( i -- )
t->valr = __carc_sub2(c, t->valr, i);

mul ( i -- )
t->valr = __carc_mul2(c, t->valr, i);

div ( i -- )
t->valr = __carc_div2(c, t->valr, i);

cons ( i -- )
t->valr = cons(c, t->valr, i);

car ( -- )
t->valr = car(t->valr);

cdr ( -- )
t->valr = cdr(t->valr);

scar ( inc -- )
scar(t->valr, inc);

scdr ( inc -- )
scdr(t->valr, inc);

is ( i -- )
t->valr = carc_is(c, t->valr, i);

